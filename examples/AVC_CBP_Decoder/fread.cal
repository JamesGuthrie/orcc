// fread.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//
// Copyright (c) 2004-2005 Xilinx Inc.
// All Rights Reserved
//
// Disclaimer:  THIS PROGRAM IS PROVIDED "AS IS" WITH NO WARRANTY 
//              WHATSOEVER AND XILINX SPECIFICALLY DISCLAIMS ANY 
//              IMPLIED WARRANTIES OF MERCHANTABILITY, FITNESS FOR
//              A PARTICULAR PURPOSE, OR AGAINST INFRINGEMENT.

/* 
   Byte source for testbed. Reads from file until EOF.
   This actor is "time-throttled", i.e. there is a simulation-time delay between
   firings. The effect is that it will only provide the next token after all
   instantaneous activity in the system, i.e. all actions requiring zero
   simulation time, has died.
*/

actor fread( String fname ) ==> int O, bool EOF :

  fd = openFile(fname);
  int nextc := readByte( fd );
  bool flag_EOF := true;

  initialize ==>
  do
    println("***************************************");
  end

  action ==> O:[v]
  guard
    nextc >= 0
    delay 1
  var
    int v
  do
    v := nextc;
    nextc := readByte( fd );
  end
  
  action ==> EOF:[true]
  guard
    nextc < 0,
    flag_EOF
  do
    flag_EOF := false;
    println("End of file");
  end
end
