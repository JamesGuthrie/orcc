/********************************************************************************

This software module was originally developed by 
Florian Decologne (IETR/INSA of Rennes), 
in the course of development of the ISO/IEC 23002-4 (Video Tool Library) 
for reference purposes and its performance may not have been optimized. 
This software module is an implementation of one or more tools as 
specified by the ISO/IEC 23002-4.

ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright 
license to copy, distribute, and make derivative works of this software 
module or modifications thereof for use in implementations of the 
ISO/IEC 23002-4 in products that satisfy conformance criteria (if any). 

Those intending to use this software module in products are advised that 
its use may infringe existing patents. ISO/IEC have no liability for use 
of this software module or modifications thereof.

Copyright is not released for products that do not conform to audiovisual 
and image-coding related ITU Recommendations and/or ISO/IEC International 
Standards. 

Assurance that the originally developed software module can be used 
(1) in the ISO/IEC 23002-4 once the ISO/IEC 23002-4 has been adopted; and 
(2) to develop the ISO/IEC 23002-4: 

IETR/INSA of Rennes grants ISO/IEC all rights necessary to 
include the originally developed software module or modifications thereof 
in the ISO/IEC 23002-4 and to permit ISO/IEC to offer You a royalty-free, 
worldwide, non-exclusive, copyright license to copy, distribute, and make 
derivative works for use in implementations of the ISO/IEC 23002-4 in 
products that satisfy conformance criteria (if any), and to the extent 
that such originally developed software module or portions of it are 
included in the ISO/IEC 23002-4.  To the extent that IETR/INSA of Rennes
owns patent rights that would be required to make, use, or 
sell the originally developed software module or portions thereof 
included in the ISO/IEC 23002-4 in a conforming product, IETR/INSA of
Rennes will assure the ISO/IEC that it is willing to 
negotiate licenses under reasonable and non-discriminatory terms and 
conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications 
thereof for the sole  purpose of developing theISO/IEC 23002-4

IETR/INSA of Rennes retains full right to modify and use the 
code for its own purpose, assign or donate the code to a third party and 
to inhibit third parties from using the code for products that do not 
conform to MPEG-related ITU Recommendations and/or ISO/IEC International 
Standards. 

This copyright notice must be included in all copies or derivative works. 
Copyright (c) ISO/IEC 2008.
*****************************************************************************/

//Modified by Mederic Blestel and Endri Bezati IETR/INSA of Rennes
//Modified by Mickael Raulet IETR/INSA of Rennes

//To do : 
//        delete ValidxRefList
//        delete ValidxRefList2
//        delete NbPOC2
//        delete IdxFalsePic
//        delete IdxNotFound42
actor Algo_RefList ()
	uint(size=31) RefReordering,
	uint(size=31) FraNum,
	uint(size=31) MaxFrameNum,
	uint(size=31) LongTermList,
	uint(size=5)  NbRefFrame,
	int (size=32) DelList,
	int (size=32) DelFalseFrameList,
	uint(size=32) POC,
	bool          IsIDR
		==>
	int (size=32) RefList
		:

/*
*
* Global variables
*
*/

	List(type:int (size=32), size=32) FrameNumList := [ 0 :for int s in Integers(0,31)];
	List(type:int (size=32), size=32) IdxLTList    := [-1 :for int s in Integers(0,31)];
	List(type:uint(size=32), size=32) POCList      := [ 0 :for int s in Integers(0,31)];

	List(type:uint(size=5) , size=32) RefIdx       := [ 0 :for int s in Integers(0,31)];
	List(type:int (size=32), size=32) FrameNumWrap := [-1 :for int s in Integers(0,31)];
	List(type:int (size=6) , size=16) ValLTList    := [-1 :for int s in Integers(0,15)];


	int (size=32) ValMovedFrame            := 0;
	uint(size=31) ValMaxFrameNum           := 0;
	uint(size=4)  IdxInsertFrame           := 0;
	uint(size=5)  CounterRefListTab        := 0; /* Contains the number of RefList's element which are sent                   */
	uint(size=5)  NbLTRead                 := 0; /* Contains the number of Long Term List's' element which are read           */
	uint(size=5)  NumMaxListRefFrame       := 0; /* Frame Number of all pictures must be between 0 and NumMaxListRefFrame - 1 */
	int (size=32) SzListDelFrame           := 0;
	int (size=32) SzListDelFalseFrame      := 0;
	int (size=32) CounterListDelList       := 0;
	int (size=32) CounterListDelFalseFrame := 0;
	uint(size=5)  IdxCurrentPic            := 0;
	uint(size=5)  IdxFalsePic              := 0;
	uint(size=6)  NbPOC                    := 0;
	int (size=32) LastFrameNum             := 0;

	uint(size=4) ValidxRefList             := 0;
	
	bool          IdxNotFound42            := true;

	procedure InsertPoc(uint(size=6) IdxPOC, uint(size=32) ValPOC, int(size=32) ValFrameNum, int(size=32) ValIdxLT)
		var
			uint(size=6) ValIdxMax
		begin
			if(NbPOC < 32) then
				ValIdxMax := NbPOC;
			else
				ValIdxMax := 31;
			end
			foreach int i in Integers(0, ValIdxMax - IdxPOC - 1)
			do
				POCList     [ValIdxMax - i] := POCList     [ValIdxMax - i - 1];
				FrameNumList[ValIdxMax - i] := FrameNumList[ValIdxMax - i - 1];
				IdxLTList   [ValIdxMax - i] := IdxLTList   [ValIdxMax - i - 1];
			end
			POCList     [IdxPOC] := ValPOC;
			FrameNumList[IdxPOC] := ValFrameNum;
			IdxLTList   [IdxPOC] := ValIdxLT;

			NbPOC := NbPOC + 1;
			if(NbPOC > 32) then
				NbPOC := 32;
			end
		end


	procedure DelPOC(uint(size=32) ValPOC)
		var
			int(size=7) IdxToDel := -1
		begin
			foreach int i in Integers(0, NbPOC - 1)
			do
				if(POCList[i] = ValPOC)then
					IdxToDel := i;
				end
			end
			foreach int i in Integers(0,NbPOC - IdxToDel - 2)
			do
				POCList     [IdxToDel + i] := POCList     [IdxToDel + i + 1];
				FrameNumList[IdxToDel + i] := FrameNumList[IdxToDel + i + 1];
				IdxLTList   [IdxToDel + i] := IdxLTList   [IdxToDel + i + 1];
			end
			NbPOC := NbPOC - 1;
			if(IdxToDel <= IdxCurrentPic and IdxCurrentPic > 0) then
				IdxCurrentPic := IdxCurrentPic - 1;
			end
		end

/*
*
* Actions
*
*/

	ReadSizeDelLists: action
			DelList           :[SizeDelListRealFrame],
			DelFalseFrameList :[SizeDelListFalseFrame]
				==>
		do
			SzListDelFrame      := SizeDelListRealFrame;
			SzListDelFalseFrame := SizeDelListFalseFrame;
			CounterListDelList       := 0;
			CounterListDelFalseFrame := 0;
			if(SzListDelFrame = -1) then
				FrameNumList[0]:= 0;
				IdxLTList   [0]:= IdxLTList   [IdxCurrentPic];
				POCList     [0]:= 0;
				IdxCurrentPic  := 0;
				NbPOC          := 1;
			end
		end


	ReadDelList.RealFrame: action
			DelList :[ValDelList]
				==>
		guard
			CounterListDelList < SzListDelFrame
		var
			uint(size=6) Idx := 0,
			uint(size=6)  NbPOC2
		do
			NbPOC2 := NbPOC;
			while(Idx < NbPOC2)
			do
				if(FrameNumList[Idx] = ValDelList)then
					DelPOC( POCList[Idx] );
				else
					Idx := Idx + 1;
				end
				NbPOC2 := NbPOC;
			end
			CounterListDelList := CounterListDelList + 1;
		end


	ReadDelList.FalseFrame: action
			DelFalseFrameList :[ValDelList]
				==>
		guard
			CounterListDelFalseFrame < SzListDelFalseFrame
		var
			uint(size=6) Idx := 0,
			uint(size=6) NbPOC2
		do
			NbPOC2 := NbPOC;
			while(Idx < NbPOC2)
			do
				if(FrameNumList[Idx] = ValDelList)then
					DelPOC( POCList[Idx] );
				else
					Idx := Idx + 1;
				end
				NbPOC2 := NbPOC;
			end
			CounterListDelFalseFrame := CounterListDelFalseFrame + 1;
		end

	ReadDelList.done: action ==>
		end

	//Gets slice's FrameNum and slice's MaxFrameNum
	read_num_ref_frames: action
			FraNum      :[frame_num],
			POC         :[ValPOC],
			MaxFrameNum :[MaxFrNu],
			IsIDR       :[IsIDR_Pic]
				==>
		var
			bool IdxNotFound := true
		do
			LastFrameNum := FrameNumList[IdxCurrentPic];
			ValMaxFrameNum := MaxFrNu;

			IdxCurrentPic := 0;
			while(IdxNotFound)
			do
				if((FrameNumList[IdxCurrentPic] > frame_num) or (IdxCurrentPic = NbPOC)) then
					IdxNotFound := false;
				end			
				IdxCurrentPic := IdxCurrentPic + 1;
			end
			IdxCurrentPic   := IdxCurrentPic   - 1;

			if(IdxCurrentPic >= 32) then
				IdxCurrentPic := 31;
			end

			if(not IsIDR_Pic) then
				InsertPoc(IdxCurrentPic,ValPOC,frame_num,-1);
			else
				InsertPoc(IdxCurrentPic,0,frame_num,-1);
			end

			foreach int i in Integers(0,31)
			do
				IdxLTList[i] := -1;
			end
			NbLTRead       := 0;
//		println("");
//		println("Frame num ::"+frame_num);	
		end


	//If the new frame num isn't following the previous frame num,
	//we must fill the short term list with all the missed frame num
	FillFalseRefFrame.launch : action ==>
		var
			uint(size=31) false_frame_num := (LastFrameNum + 1) mod ValMaxFrameNum,
			bool          IdxNotFound     := true
		do
			while(false_frame_num != FrameNumList[IdxCurrentPic])
			do
			
				IdxFalsePic := 0;
				IdxNotFound:= IdxNotFound42;
				while(IdxNotFound)
				do
					if((FrameNumList[IdxFalsePic] > false_frame_num) or (IdxFalsePic = NbPOC)) then
						IdxNotFound42 := false;
					end			
					IdxFalsePic := IdxFalsePic + 1;
					IdxNotFound :=IdxNotFound42;
				end
				IdxNotFound42 := true;
				IdxFalsePic   := IdxFalsePic   - 1;
				InsertPoc(IdxFalsePic,42,false_frame_num,-1);
				if(IdxFalsePic <= IdxCurrentPic and IdxCurrentPic < 31) then
					IdxCurrentPic := IdxCurrentPic + 1;
				end
				false_frame_num        := (false_frame_num + 1) mod ValMaxFrameNum;
			end
		end


	FillFalseRefFrame.done : action ==>
		guard
			(FrameNumList[IdxCurrentPic] = (LastFrameNum + 1) mod ValMaxFrameNum) or (FrameNumList[IdxCurrentPic] = LastFrameNum)
		end


	//Fill the Long Term List
	ReadLongTermList.launch: action
			LongTermList :[ValLongTerm]
				==>
		guard
			NbLTRead < 16
		do
			foreach int i in Integers(0,NbPOC)
			do
				if(FrameNumList[i] = ValLongTerm) then
					IdxLTList[i] := NbLTRead;
				end
			end
			NbLTRead     := NbLTRead + 1;
		end


	//Long Term and Short Term are read
	ReadLongTermList.done: action ==>
		end


	//Get the Length of the Reference Frame (and send it throw RefList) and initialize the RefListTab
	InitRefList: action
		NbRefFrame :[ValIDxMax]
			==>
		RefList      :[NumMaxListRefFrame]
	var
			uint(size=4) IdxRefList2,
			uint(size=5) IdxBuffer,
			int (size=32) FrameNumBuffer
	do
		//IdxCurrentPic = Nb short term and long term ref
		if(ValIDxMax < NbPOC - 1) then
			NumMaxListRefFrame := ValIDxMax;
		else
			NumMaxListRefFrame := NbPOC - 1;
		end

		foreach int i in Integers(0,15)
		do
			ValLTList[i] := -1;
		end
ValidxRefList := 0;//////
		foreach int i in Integers (0,NbPOC - 1)
		do
			IdxRefList2 := ValidxRefList; 
			if(i != IdxCurrentPic)then
				if(IdxLTList[i] = -1)then
					RefIdx[IdxRefList2] := i;
					if(FrameNumList[i]>FrameNumList[IdxCurrentPic]) then
						FrameNumWrap[IdxRefList2] := FrameNumList[i] - ValMaxFrameNum;
					else
						FrameNumWrap[IdxRefList2] := FrameNumList[i];
					end
					IdxRefList2         := IdxRefList2 + 1;
				else
					ValLTList[IdxLTList[i]] := i;
				end
			end
			ValidxRefList := IdxRefList2;
		end
		if(ValidxRefList>0) then
			foreach int i in Integers(0,ValidxRefList - 1)
			do
				foreach int j in Integers(i + 1,ValidxRefList - 1)
				do
					if(FrameNumWrap[i] < FrameNumWrap[j])then
						IdxBuffer       := RefIdx[j];
						RefIdx[j]       := RefIdx[i];
						RefIdx[i]       := IdxBuffer;

						FrameNumBuffer  := FrameNumWrap[j];
						FrameNumWrap[j] := FrameNumWrap[i];
						FrameNumWrap[i] := FrameNumBuffer;
					end
				end
			end
		end
		if(ValidxRefList < NumMaxListRefFrame)then
			foreach int i in Integers(0,15)
			do
				IdxRefList2 := ValidxRefList; 
				if ( (ValLTList[i] != -1) and (IdxRefList2 < NumMaxListRefFrame) ) then 
					RefIdx[IdxRefList2] := ValLTList[i]; 
					IdxRefList2         := IdxRefList2 + 1 ;
				end
				ValidxRefList := IdxRefList2;
			end
		end
		
		//Indicates that nothing in RefList has been sent
		CounterRefListTab := 0;
		IdxInsertFrame    := 0;
	end


	//If we don't have to reorder the list, we jump to SendRefList (see scheduler)
	read_ref_pic_list_reordering_flag_0: action 
			RefReordering :[reordering_flag]
				==>
		guard
			reordering_flag = 0
		end


	//We have to reorder the list
	read_ref_pic_list_reordering_flag_1: action 
			RefReordering :[reordering_flag]
				==> 
		guard
			reordering_flag = 1
		do
			ValMovedFrame  := FrameNumList[IdxCurrentPic];
		end


	//A Short Term Reference must be reordered
	read_reordering_of_pic_nums_idc_0or1: action
			RefReordering :[reordering_of_pic_nums_idc, abs_diff_pic_num_minus1]
				==>
		guard 
			(reordering_of_pic_nums_idc = 0 or reordering_of_pic_nums_idc = 1) 
		var 
			int ValMovedIdx                 := 0
			
		do
			//Compute the value of the re-mapped reference picture
			if (reordering_of_pic_nums_idc = 0) then 
				ValMovedFrame := ValMovedFrame - abs_diff_pic_num_minus1 - 1 ; 
				if(ValMovedFrame < 0) then
					ValMovedFrame := ValMovedFrame + ValMaxFrameNum;
				end
			else // if(a =1) then
				ValMovedFrame := ValMovedFrame + abs_diff_pic_num_minus1 + 1;
				if(ValMovedFrame >= ValMaxFrameNum) then
					ValMovedFrame := ValMovedFrame - ValMaxFrameNum;
				end
			end
		ValidxRefList:=0;
			//Search the location of the reference picture (ValMovedFrame) in the RefList
			foreach int i in Integers(0, NumMaxListRefFrame-1)
			do
				if (FrameNumList[RefIdx[i]] = ValMovedFrame) then 
					ValidxRefList  := i;
					ValMovedIdx := RefIdx[i];
				end
			end
			if(ValidxRefList > IdxInsertFrame) then
				//Shift to the right all the element with an index between 
				//IdxInsertFrame and idxRefList - 1 (IdxInsertFrame and idxRefList - 1 are also shifted).
				//Thanks to this shift, the short term reference picture located at idxRefList is deleted.
				foreach int i in Integers (0, ValidxRefList - IdxInsertFrame - 1)
				do
					RefIdx[ValidxRefList - i] := RefIdx[ValidxRefList - i - 1];
				end
				//Put the short term ref picture in his new position
				RefIdx[IdxInsertFrame] := ValMovedIdx;
			end
			//The next reference picture will be insert next to this ref picture
			IdxInsertFrame             := IdxInsertFrame +1 ;
		end


	//A Long Term Reference must be reordered
	read_reordering_of_pic_nums_idc_2: action
			RefReordering:[reordering_of_pic_nums_idc, idx_LTList]
				==>
		guard 
			reordering_of_pic_nums_idc = 2
		do
			ValidxRefList := 0;
			//Search the location of the reference picture in the RefList
			foreach int i in Integers(0, NumMaxListRefFrame-1)
			do
				if (RefIdx[i] = ValLTList[idx_LTList]) then 
					ValidxRefList := i;
				end
			end
			//Shift to the right all the element with an index between 
			//IdxInsertFrame and idxRefList - 1 (IdxInsertFrame and idxRefList - 1 are also shifted).
			//Thanks to this shift, the long term reference picture located at idxRefList is deleted.
			if(ValidxRefList > IdxInsertFrame) then
				foreach int i in Integers (0, ValidxRefList - IdxInsertFrame - 1)
				do
					RefIdx[ValidxRefList - i] := RefIdx[ValidxRefList - i - 1];
				end
			end
			//Put the long term ref picture in his new position
			RefIdx[IdxInsertFrame] := ValLTList[idx_LTList];
			//The next reference picture will be insert next to this ref picture
			IdxInsertFrame             := IdxInsertFrame + 1 ;
		end


	//Reordering is completed
	read_reordering_of_pic_nums_idc_3: action
			RefReordering:[reordering_of_pic_nums_idc]
				==>
		guard 
			reordering_of_pic_nums_idc = 3
		end

	//Send the RefList
	SendRefList: action 
				==>
			RefList:[poc]
		guard
			CounterRefListTab < NumMaxListRefFrame 
		var
			int (size = 32) poc := -1
		do
//println("poc:"+POCList[RefIdx[CounterRefListTab]]+"; frnum:"+FrameNumList[RefIdx[CounterRefListTab]]+"; idx"+RefIdx[CounterRefListTab]);
			poc := POCList[RefIdx[CounterRefListTab]];
			CounterRefListTab := CounterRefListTab + 1;
		end

	//The RefList is sent
	SendRefListDone: action ==>
		end

/*
*
* Scheduler
*
*/
	schedule fsm  read_num_ref_frames:
		ReadSizeDelLists                  (ReadSizeDelLists                     )--> ReadDelLists;
		ReadDelLists                      (ReadDelList.RealFrame                )--> ReadDelLists;
		ReadDelLists                      (ReadDelList.FalseFrame               )--> ReadDelLists;
		ReadDelLists                      (ReadDelList.done                     )--> read_num_ref_frames;

		read_num_ref_frames               (read_num_ref_frames                  )--> FillFalseRefFrame;
		FillFalseRefFrame                 (FillFalseRefFrame                    )--> ReadLongTerm;

		ReadLongTerm                      (ReadLongTermList.launch              )--> ReadLongTerm;
		ReadLongTerm                      (ReadLongTermList.done                )--> InitRefList;

		InitRefList                       (InitRefList                          )--> read_ref_pic_list_reordering_flag;
		read_ref_pic_list_reordering_flag (read_ref_pic_list_reordering_flag_0  )--> SendRefList;

		read_ref_pic_list_reordering_flag (read_ref_pic_list_reordering_flag_1  )--> read_reordering_of_pic_nums_idc;
		read_reordering_of_pic_nums_idc   (read_reordering_of_pic_nums_idc_0or1 )--> read_reordering_of_pic_nums_idc;
		read_reordering_of_pic_nums_idc   (read_reordering_of_pic_nums_idc_2    )--> read_reordering_of_pic_nums_idc;
		read_reordering_of_pic_nums_idc   (read_reordering_of_pic_nums_idc_3    )--> SendRefList;

		SendRefList                       (SendRefList                          )--> SendRefList;
		SendRefList                       (SendRefListDone                      )--> ReadSizeDelLists;


	end


	priority
		ReadDelList.RealFrame   > ReadDelList.FalseFrame    > ReadDelList.done;
		ReadLongTermList.launch > ReadLongTermList.done;
		SendRefList             > SendRefListDone;
		FillFalseRefFrame.done  > FillFalseRefFrame.launch;
	end

end
