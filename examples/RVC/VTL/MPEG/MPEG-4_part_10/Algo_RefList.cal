/********************************************************************************

This software module was originally developed by 
Florian Decologne (IETR/INSA of Rennes), 
in the course of development of the ISO/IEC 23002-4 (Video Tool Library) 
for reference purposes and its performance may not have been optimized. 
This software module is an implementation of one or more tools as 
specified by the ISO/IEC 23002-4.

ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright 
license to copy, distribute, and make derivative works of this software 
module or modifications thereof for use in implementations of the 
ISO/IEC 23002-4 in products that satisfy conformance criteria (if any). 

Those intending to use this software module in products are advised that 
its use may infringe existing patents. ISO/IEC have no liability for use 
of this software module or modifications thereof.

Copyright is not released for products that do not conform to audiovisual 
and image-coding related ITU Recommendations and/or ISO/IEC International 
Standards. 

Assurance that the originally developed software module can be used 
(1) in the ISO/IEC 23002-4 once the ISO/IEC 23002-4 has been adopted; and 
(2) to develop the ISO/IEC 23002-4: 

IETR/INSA of Rennes grants ISO/IEC all rights necessary to 
include the originally developed software module or modifications thereof 
in the ISO/IEC 23002-4 and to permit ISO/IEC to offer You a royalty-free, 
worldwide, non-exclusive, copyright license to copy, distribute, and make 
derivative works for use in implementations of the ISO/IEC 23002-4 in 
products that satisfy conformance criteria (if any), and to the extent 
that such originally developed software module or portions of it are 
included in the ISO/IEC 23002-4.  To the extent that IETR/INSA of Rennes
owns patent rights that would be required to make, use, or 
sell the originally developed software module or portions thereof 
included in the ISO/IEC 23002-4 in a conforming product, IETR/INSA of
Rennes will assure the ISO/IEC that it is willing to 
negotiate licenses under reasonable and non-discriminatory terms and 
conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications 
thereof for the sole  purpose of developing theISO/IEC 23002-4

IETR/INSA of Rennes retains full right to modify and use the 
code for its own purpose, assign or donate the code to a third party and 
to inhibit third parties from using the code for products that do not 
conform to MPEG-related ITU Recommendations and/or ISO/IEC International 
Standards. 

This copyright notice must be included in all copies or derivative works. 
Copyright (c) ISO/IEC 2008.
*****************************************************************************/

//Modified by Mederic Blestel and Endri Bezati IETR/INSA of Rennes
//Modified by Mickael Raulet IETR/INSA of Rennes


actor Algo_RefList () 
	uint(size=32)  RefReordering,
	uint(size=31)  FraNum,
	uint(size=31) MaxFrameNum,
	uint(size=31) ShortTermList,
	uint(size=5)  SzSTList,
	uint(size=31) LongTermList,
	uint(size=5) Nb_LT_Frame,
	uint(size=5) Nb_RefFrame
		==>
	int(size=32) RefList
		:


	List(type:int(size=32),size=16)         reordering_ref_list := [0 :for int s in Integers(0,16-1)];
	List(type:int(size=32),size=16)         STList     := [0 :for int s in Integers(0,16-1)];
	List(type:int,size=16)                  pic_idx_list        := [0 :for int s in Integers(0,16-1)];
	List(type:int(size=32),size=16)         LTList      := [-1 :for int s in Integers(0,16-1)];
	List(type:uint(size = 32), size = 2*16) RefListTab          := [-1 :for int s in Integers(0,2*16-1)];
	List(type:uint(size=32),size= 2*16)     RefListSent         := [0: for int s in Integers(0, 2*16-1)];

	uint          frame_num          := 0;
	int           pred               := 0;
	int (size=32) ValMaxFrameNum     := 0;
	uint(size=4)  IdxInsertFrame     := 0;
	uint(size=5)  ShortTermCount   := 0;
	uint(size=5)  LongTermCount    := 0;
	int           CounterRefListTab  := 0;/**/
	bool          EndInitIdc3        := false;/**/
	bool          EndInitSentRefList := false;/**/
	uint(size=5)  NbSTRead := 0;
	uint(size=5)  NbLTRead  := 0;
	uint(size=5)  NumMaxListRefFrame := 0;


	read_num_ref_frames: action
			FraNum:[b],
			MaxFrameNum:[MaxFrNu]
				==>
		do
			frame_num      := b;
			ValMaxFrameNum := MaxFrNu;
		end


	read_ref_pic_list_reordering_flag_0: action 
			RefReordering:[v],
			Nb_RefFrame:[ValIDxMax]
				==>
		guard
			v = 0
		do
			if(ValIDxMax < ShortTermCount + LongTermCount) then
				NumMaxListRefFrame := ValIDxMax;
			else
				NumMaxListRefFrame := ShortTermCount + LongTermCount;
			end
		end


	read_ref_pic_list_reordering_flag_1: action 
			RefReordering:[v],
			Nb_RefFrame:[ValIDxMax]
				==> 
		guard
			v = 1
		var 
			int ListIndex := 0,
			int(size=32) frame_num_short_term
		do
			if(ValIDxMax < ShortTermCount + LongTermCount) then
				NumMaxListRefFrame := ValIDxMax;
			else
				NumMaxListRefFrame := ShortTermCount + LongTermCount;
			end
			pred  := frame_num;
			IdxInsertFrame := 0;
			if (ShortTermCount != 0) then
				foreach int i in Integers (0, ShortTermCount - 1)
				do
					reordering_ref_list[ListIndex] := STList[i];
					ListIndex                      := ListIndex + 1 ;
				end
			else 
				reordering_ref_list := [0 :for int s in Integers(0,16-1)];
			end 
			if (LongTermCount != 0) then 
				foreach int i in Integers(0, 15)
				do
					if ( (LTList[i] != -1) and (ListIndex <= 16) ) then 
						reordering_ref_list[ListIndex] := LTList[i]; 
						ListIndex                      := ListIndex + 1 ;
					end
				end
			end
		end

  
	read_reordering_of_pic_nums_idc_01: action
			RefReordering:[a, b]
				==>
		guard 
			(a = 0 or a = 1) 
		var 
			uint(size=4) idx         := 0,
			int          counter_for := 0
		do
			if (a = 0) then 
				pred := pred - b - 1 ; 
				if(pred < 0) then
					pred := pred + ValMaxFrameNum;
				end
			else // if(a =1) then
				pred := pred + b+1;
				if(pred >= ValMaxFrameNum) then
					pred := pred - ValMaxFrameNum;
				end
			end
			foreach int i in Integers(0, ShortTermCount+LongTermCount-1)
			do
				if (reordering_ref_list[i] = pred) then 
					idx := i; 
				end
			end
			//Remove from the reference list the frame at IdxInsertFrame equals to idx
			foreach int i in Integers(idx, ShortTermCount+LongTermCount - 2)
			do
				reordering_ref_list[i] := reordering_ref_list[i + 1];
			end

			//Add a frame at IdxInsertFrame equals to IdxInsertFrame.
			//So copy the IdxInsertFrame - 1 first elements
			//Add pred_ix at IdxInsertFrame "IdxInsertFrame"
			//and copy the others elements of the list
			counter_for:=ShortTermCount + LongTermCount - 1;
			foreach int i in Integers(0, counter_for - IdxInsertFrame)
			do
				reordering_ref_list[counter_for - i] := reordering_ref_list[counter_for - i - 1];
			end
			reordering_ref_list[IdxInsertFrame] := pred;
			IdxInsertFrame := IdxInsertFrame +1 ;
		end


	read_reordering_of_pic_nums_idc_2: action
			RefReordering:[a, b]
				==>
		guard 
			a = 2
		var 
			uint(size=32) idx,
			int counter_for  
		do
			foreach int i in Integers(0, ShortTermCount+LongTermCount-1)
			do
				if (reordering_ref_list[i] = LTList[b]) then 
					idx := i;
				end
			end

			//Remove from the reference list the frame at IdxInsertFrame equals to idx
			foreach int i in Integers(idx, ShortTermCount+LongTermCount - 2)
			do
				reordering_ref_list[i] := reordering_ref_list[i + 1];
			end 
			
			//Add a frame at IdxInsertFrame equals to IdxInsertFrame.
			//So copy the IdxInsertFrame - 1 first elements
			//Add pred_ix at IdxInsertFrame "IdxInsertFrame"
			//and copy the others elements of the list
			counter_for:=ShortTermCount + LongTermCount - 1;
			foreach int i in Integers(0, counter_for - IdxInsertFrame)
			do
				reordering_ref_list[counter_for - i] := reordering_ref_list[counter_for - i - 1];
			end
			reordering_ref_list[IdxInsertFrame] := LTList[b];
			
			reordering_ref_list[IdxInsertFrame] := LTList[b];
			IdxInsertFrame := IdxInsertFrame + 1 ;
		end


	read_reordering_of_pic_nums_idc_3: action
			RefReordering:[a]
				==>
		guard 
			a = 3
		do
			RefListTab[0] := NumMaxListRefFrame;
			foreach int s in Integers(0, NumMaxListRefFrame - 1)
			do
				RefListTab[s + 1] := reordering_ref_list[s];
			end
			EndInitIdc3 := true;
		end


	IDC3SendRefList: action 
				==>
			RefList:[ref]
		guard
			EndInitIdc3
		var
			uint(size = 5) ref
		do
			ref := RefListTab[CounterRefListTab];
			CounterRefListTab := CounterRefListTab + 1;
		end		


	IDC3SendRefListDone: action ==>
		guard
			CounterRefListTab = NumMaxListRefFrame + 1
		do
			CounterRefListTab := 0;
			EndInitIdc3       := false;
		end	


	InitRefList_zero: action 
				==>
			RefList:[0]
		guard
			(ShortTermCount + LongTermCount) = 0
		do
			EndInitSentRefList := false;
		end


	InitRefList_not0: action ==> 
		guard
			(EndInitSentRefList = false)  and  ( ShortTermCount + LongTermCount != 0 )
		var 
			List(type:uint(size=32),size=2*16) reflist := [0: for int k in Integers(0,2*16-1)],
			int ListIndex := 0
		do
			if (ShortTermCount != 0) then 
				foreach int l in Integers( 0, ShortTermCount - 1 )
				do
					reflist[l] := STList[l];
				end
				ListIndex      := ShortTermCount; 
			else // => inutile !
				foreach int s in Integers( 0, ShortTermCount + LongTermCount - 1 )
				do
					reflist[s] := 0;
				end
				ListIndex := ShortTermCount + LongTermCount; 
			end
			if (LongTermCount != 0) then
				foreach int i in Integers(0, 15)
				do
					if (LTList[i] != -1) then 
						reflist[ListIndex] := LTList[i];
						ListIndex          := ListIndex + 1;
					end
				end
			end
			RefListSent[0] := NumMaxListRefFrame;
			foreach int s in Integers(0, NumMaxListRefFrame - 1)
			do
				RefListSent[s + 1] := reflist[s];	
			end
			EndInitSentRefList     := true;	
		end


	SendRefList: action
				==>
			RefList:[ref]
		guard
			EndInitSentRefList = true
		var
			uint(size = 32) ref
		do
			ref := RefListSent[CounterRefListTab];
			CounterRefListTab := CounterRefListTab + 1;
		end


	SendRefListDone: action ==>
		guard
			CounterRefListTab = NumMaxListRefFrame + 1
		do
			CounterRefListTab  := 0;
			EndInitSentRefList := false;
		end


	ReadSizeLists: action
			SzSTList    :[size_short_term_list],
			Nb_LT_Frame :[val_long_term_count]
				==>
		do
			ShortTermCount   := size_short_term_list;
			LongTermCount    := val_long_term_count;
			NbSTRead := 0;
			NbLTRead := 0;
		end


	ReadShortTermList: action
			ShortTermList :[ValShortTerm]
				==>
		guard
			NbSTRead < ShortTermCount
		do
			STList[NbSTRead] := ValShortTerm;
			NbSTRead         := NbSTRead + 1;
		end


	ReadLongTermList: action
			LongTermList :[ValLongTerm]
				==>
		guard
			NbLTRead < 16
		do
			LTList[NbLTRead] := ValLongTerm;
			NbLTRead         := NbLTRead + 1;
		end

	
	ReadListsDone: action ==>
		end

	schedule fsm  read_num_ref_frames:
		read_num_ref_frames               (read_num_ref_frames                 )--> ReadSizeLists;

		ReadSizeLists                     (ReadSizeLists                       )-->ReadLists;
		ReadLists                 (ReadShortTermList                   )-->ReadLists;
		ReadLists                  (ReadLongTermList                    )-->ReadLists;
		ReadLists                  (ReadListsDone               )-->read_ref_pic_list_reordering_flag;

		/* there isn't list to sent */
		read_ref_pic_list_reordering_flag (read_ref_pic_list_reordering_flag_0 )--> send_refList;
		send_refList                      (InitRefList_zero                    )--> read_num_ref_frames;
		send_refList                      (InitRefList_not0                    )--> RefListTabSent;
		RefListTabSent                    (SendRefList                         )--> RefListTabSent;
		RefListTabSent                    (SendRefListDone                     )--> read_num_ref_frames;

		/* there is list 0 to sent */
		read_ref_pic_list_reordering_flag (read_ref_pic_list_reordering_flag_1 )--> read_reordering_of_pic_nums_idc;
		read_reordering_of_pic_nums_idc   (read_reordering_of_pic_nums_idc_01  )--> read_reordering_of_pic_nums_idc;
		read_reordering_of_pic_nums_idc   (read_reordering_of_pic_nums_idc_2   )--> read_reordering_of_pic_nums_idc;
		read_reordering_of_pic_nums_idc   (read_reordering_of_pic_nums_idc_3   )--> IDC3SendRefList;
		IDC3SendRefList                   (IDC3SendRefList                     )--> IDC3SendRefList;
		IDC3SendRefList                   (IDC3SendRefListDone                 )--> read_num_ref_frames;


	end

	priority
		SendRefListDone      > SendRefList;
		IDC3SendRefListDone  > IDC3SendRefList;
		ReadShortTermList    > ReadListsDone;
		ReadLongTermList     > ReadListsDone;
	end

end
