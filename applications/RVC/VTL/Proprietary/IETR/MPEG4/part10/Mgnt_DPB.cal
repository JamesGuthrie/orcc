/********************************************************************************

This software module was originally developed by Florian Decologne IETR/INSA of Rennes (France), in the course of development of the <standard> for reference purposes and its performance may not have been optimized. This software module is an implementation of one or more tools as specified by the MPEG-C part 4.
ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works of this software module or modifications thereof for use in implementations of the <standard>  in products that satisfy conformance criteria (if any).
Those intending to use this software module in products are advised that its use may infringe existing patents. ISO/IEC have no liability for use of this software module or modifications thereof.
Copyright is not released for products that do not conform to audiovisual and image-coding related ITU Recommendations and/or ISO/IEC International Standards.
Assurance that the originally developed software module can be used (1) in the <standard> once the <standard> has been adopted; and (2) to develop the MPEG-C part 4:

IETR/INSA of Rennes (France) grants ISO/IEC all rights necessary to include the originally developed software module or modifications thereof in the <standard> and to permit ISO/IEC to offer You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works for use in implementations of the MPEG-C part 4 in products that satisfy conformance criteria (if any), and to the extent that such originally developed software module or portions of it are included in the MPEG-C part 4.  To the extent that IETR/INSA of Rennes (France) owns patent rights that would be required to make, use, or sell the originally developed software module or portions thereof included in the MPEG-C part 4 in a conforming product,  IETR/INSA of Rennes (France) will assure the ISO/IEC that it is willing to negotiate licenses under reasonable and non-discriminatory terms and conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications thereof for the sole  purpose of developing the MPEG-C part 4

IETR/INSA of Rennes (France) retains full right to modify and use the code for its own purpose, assign or donate the code to a third party and to inhibit third parties from using the code for products that do not conform to MPEG-related ITU Recommendations and/or ISO/IEC International Standards.
This copyright notice must be included in all copies or derivative works. Copyright (c) ISO/IEC 2008.

*****************************************************************************/
//Modified by Damien de Saint Jorre IETR/INSA of Rennes
package Proprietary.IETR.MPEG4.part10;

import MPEG.Common.Imp_Math.*;
import MPEG.Common.Imp_PictureInfo.*;

//Clean this actor (Delete and rewrite it)!!!
actor Mgnt_DPB ( int MB_WIDTH )
	uint(size=7)  PicWidthInMb,
	uint(size=7)  MbLocation,
	uint(size=13) PicSizeInMb, 
	uint          POC,
	uint(size=32) POCtoRead,
	int(size=32)  DelList,
	uint(size=8)   WD,
	bool          EnableRead,
	int (size=12) ReadAddress,
	bool          IsIDR,
	uint(size=2)  IsNewPicOrSlice,
	uint(size=31) FrameNum
		==> 
	uint(size=8)  ReadResult
		:


/*
*
* CONSTANTS
*
*/
	uint(size=2) IS_NEW_MB    = 0;
	uint(size=2) IS_NEW_PIC   = 2;

	int (size=32) SENT_DONE       = -2;

	uint(size=32) NB_PIC          = 33;
	uint(size=32) SZ_NBPIC        = 6; /* >= log2(NB_PIC) */
	//FIXME! number of MB per line of picture


	List(type :List(type: List( type:uint(size=8), size=512), size=1024), size=NB_PIC) pic;
	List(type:uint(size=32) ,size=NB_PIC) ListPOC       := [ 0    :for int s in 0 .. NB_PIC-1];
	List(type:uint(size=31) ,size=NB_PIC) ListFrameNum  := [ 0    :for int s in 0 .. NB_PIC-1];
	List(type:bool          ,size=NB_PIC) PictureReady  := [false :for int s in 0 .. NB_PIC-1];
	List(type:bool          ,size=NB_PIC) IsAReference  := [false :for int s in 0 .. NB_PIC-1];

	int (size=32) pic_curr_idx     := 0;
	uint(size=7)  PicWidthInMacroB := 0;
	uint(size=7)  PicHeightInMacroB := 0;
	uint(size=13) PicSizeInMacroB  := 0;

	/*Write information*/
	bool          write_done       := true;
	uint(size=21) wa_x_offset      := 0;
	uint(size=21) wa_y_offset      := 0;
	uint(size=11) wa_x             := 0;
	uint(size=11) wa_y             := 0;

	/*Read Information*/
	bool                read_done  := true;
	uint(size=11)       ra_W       := 0;
	uint(size=11)       ra_H       := 0;
	uint(size=11)       ra_x       := 0;
	uint(size=11)       ra_y       := 0;
	int (size=12)       ra_off_x   := 0;
	int (size=12)       ra_off_y   := 0;
	uint(size=SZ_NBPIC) Ref_idx    := 0;

	/*Delete information*/
	bool          ValIsIDR := false;

	GetIsNewPic: action 
			IsNewPicOrSlice :[is_new_mb_or_slice]
				==>
		end

	get_writeAddr: action
			MbLocation      :[MbLocxInPic , MbLocyInPic],
			PicWidthInMb    :[PICwidthInMb],
			PicSizeInMb     :[PICSizeInMb],
			IsNewPicOrSlice :[is_new_pic_or_slice]
				==>
		guard
			write_done = true,
			is_new_pic_or_slice = IS_NEW_MB
		do
			PicWidthInMacroB := PICwidthInMb;
			PicSizeInMacroB  := PICSizeInMb;
			wa_x_offset := MbLocxInPic * MB_WIDTH;
			wa_y_offset := MbLocyInPic * MB_WIDTH;
			wa_x       := 0;
			wa_y       := 0;
			write_done := false;
		end


	//Write_data: 
	write_data : action
			WD :[wd] repeat 8 
				==>
		guard 
			write_done = false
		do
			foreach int i in 0 .. 7
			do
				pic[pic_curr_idx][wa_x_offset + wa_x + i][wa_y_offset + wa_y] := wd[i];
			end
			wa_x := wa_x +8;
			if(wa_x = MB_WIDTH) then
				wa_y := wa_y +1;
				wa_x := 0;
				write_done := (wa_y = MB_WIDTH);
			end
		end

	uint(size=32) ReadTable[32][32];

	get_readAddr: action
			ReadAddress :[Ra_off_x , Ra_off_y , RAWidth , RAHeight],
			POCtoRead   :[poc2read , frame_num_to_read],
			EnableRead  :[read_enabled]
				==>
		guard
			read_done  = true,
			read_enabled
		var
			bool found := false,
			int x,
			int y,
			uint(size=32) Read_addr_x,
			uint(size=32) Read_addr_y,
			int idxYMin,
			int idxXMin
		do
			ra_off_x  := Ra_off_x;
			ra_off_y  := Ra_off_y;
			ra_W      := RAWidth;
			ra_H      := RAHeight;
			ra_x      := 0;
			ra_y      := 0;
			read_done := false;
			Ref_idx   := 0;
			foreach int i in 0 .. NB_PIC-1
			do
				if( (poc2read=ListPOC[i]) and (frame_num_to_read = ListFrameNum[i]) and (IsAReference[i] = true)) then
					Ref_idx := i;
					found := true;
				end
			end
			if(not found)then
				println("\nCurrent Poc : "+ListPOC[pic_curr_idx]+" ; Current FrameNum : "+ListFrameNum[pic_curr_idx]);
				println("POC not found :"+poc2read+" ; FrameNum : "+frame_num_to_read+"\n");
					foreach int i in 0 .. NB_PIC-1
					do
						println("Idx n"+i+"; PocVal:"+ListPOC[i]+ " ; Frame Num:"+ListFrameNum[i]+" ; is a ref:"+IsAReference[i]);
					end
				while(true)
				do
				end
			end
			idxYMin := max(-Ra_off_y,0);
			idxXMin := max(-Ra_off_x,0);

			y := 0;
			foreach int y_addr in Ra_off_y .. Ra_off_y + RAHeight
			do
				x := 0;
				foreach int x_addr in Ra_off_x .. Ra_off_x + RAWidth
				do
					Read_addr_x := min( max(x_addr , 0) , PicWidthInMacroB  * MB_WIDTH - 1);
					Read_addr_y := min( max(y_addr , 0) , PicHeightInMacroB * MB_WIDTH - 1);
					ReadTable[x][y] := pic[Ref_idx][Read_addr_x][Read_addr_y];
					x := x + 1;
				end
				y := y+1;
			end
		end

	// Read data
	read_data: action
				==>
			ReadResult :[rd]
		guard 
			read_done = false
		var 
			uint(size=8) rd
		do
			rd   := ReadTable[ra_x][ra_y];
			ra_x := ra_x +1;
			if(ra_x = ra_W) then
				ra_y := ra_y +1;
				if(ra_y != ra_H) then
					ra_x := 0; 
				else 
					ra_x := 0;
					ra_y := 0; 
					read_done := true;
				end
			end
		end

	write_picture_done: action
			IsNewPicOrSlice :[is_new_mb_or_slice]
				==>
		guard 
			write_done = true,
			is_new_mb_or_slice = IS_NEW_PIC
		do
			PictureReady[pic_curr_idx] := true;
			wa_x        := 0;
			wa_y        := 0;
			PicHeightInMacroB := PicSizeInMacroB / PicWidthInMacroB;
		end

	read_ref_parameters: action
			POC             :[Valpoc],
			FrameNum        :[frame_num],
			IsIDR           :[IsIDRPic],
			EnableRead      :[enabled_read]
				==>
		guard
			not enabled_read
		var
			bool         idx_selected := false,
			int(size=32) i            := 0
		do
			pic_curr_idx := -1;
			while((not idx_selected) and (i < NB_PIC))
			do
				if(IsAReference[i] = false) then
					idx_selected    := true;
					pic_curr_idx    := i;
					if(not IsIDRPic) then
						ListFrameNum[i] := frame_num;
						ListPOC[i]      := Valpoc;
					else
						ListFrameNum[i] := 0;
						ListPOC[i]      := 0;
					end
					IsAReference[i] := true;
					PictureReady[i] := false;
				end
				i := i + 1;
			end
			ValIsIDR := IsIDRPic;
			/*Fix Me!!! ==> exception*/
			if(pic_curr_idx = -1) then
				pic_curr_idx    := 0;
				ListPOC[0]      := Valpoc;
				ListFrameNum[0] := frame_num;
				IsAReference[0] := true;
				PictureReady[0] := false;
			end
		end

	ReadDelList.Launch: action
			DelList:[DelPoc , DelFrame]
				==>
		guard
			DelPoc != SENT_DONE
		do
			foreach int i in 0 .. NB_PIC - 1
			do
				if(ListPOC[i]   = DelPoc and ListFrameNum[i] = DelFrame) then
					IsAReference[i] := false;
				end
			end
		end


	ReadDelList.Done: action
			DelList:[DelVal]
				==>
		do
			if(ValIsIDR)then
				foreach int i in 0 .. NB_PIC - 1
				do
					if(i != pic_curr_idx) then
						IsAReference[i] := false;
					else
						ListPOC[i]      := 0;
					end
				end
			end
		end
  
	schedule fsm  GetIsNewPic:
		GetIsNewPic (GetIsNewPic )--> read_ref_parameters;

		read_ref_parameters (read_ref_parameters      )--> read_write;

		read_write          (write_data               )--> read_write;
		read_write          (read_data                )--> read_write;
		read_write          (get_writeAddr            )--> read_write;
		read_write          (get_readAddr             )--> read_write;
		read_write          (write_picture_done       )--> read_del_list;

		read_del_list       (ReadDelList.Launch      )--> read_del_list;
		read_del_list       (ReadDelList.Done        )--> read_ref_parameters;
	end
	priority
		write_picture_done         > get_writeAddr       > get_readAddr  > write_data     > read_data;
		ReadDelList.Launch         > ReadDelList.Done;
	end
end
