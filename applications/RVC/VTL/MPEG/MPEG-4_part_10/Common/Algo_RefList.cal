/********************************************************************************

This software module was originally developed by 
Florian Decologne (IETR/INSA of Rennes), 
in the course of development of the ISO/IEC 23002-4 (Video Tool Library) 
for reference purposes and its performance may not have been optimized. 
This software module is an implementation of one or more tools as 
specified by the ISO/IEC 23002-4.

ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright 
license to copy, distribute, and make derivative works of this software 
module or modifications thereof for use in implementations of the 
ISO/IEC 23002-4 in products that satisfy conformance criteria (if any). 

Those intending to use this software module in products are advised that 
its use may infringe existing patents. ISO/IEC have no liability for use 
of this software module or modifications thereof.

Copyright is not released for products that do not conform to audiovisual 
and image-coding related ITU Recommendations and/or ISO/IEC International 
Standards. 

Assurance that the originally developed software module can be used 
(1) in the ISO/IEC 23002-4 once the ISO/IEC 23002-4 has been adopted; and 
(2) to develop the ISO/IEC 23002-4: 

IETR/INSA of Rennes grants ISO/IEC all rights necessary to 
include the originally developed software module or modifications thereof 
in the ISO/IEC 23002-4 and to permit ISO/IEC to offer You a royalty-free, 
worldwide, non-exclusive, copyright license to copy, distribute, and make 
derivative works for use in implementations of the ISO/IEC 23002-4 in 
products that satisfy conformance criteria (if any), and to the extent 
that such originally developed software module or portions of it are 
included in the ISO/IEC 23002-4.  To the extent that IETR/INSA of Rennes
owns patent rights that would be required to make, use, or 
sell the originally developed software module or portions thereof 
included in the ISO/IEC 23002-4 in a conforming product, IETR/INSA of
Rennes will assure the ISO/IEC that it is willing to 
negotiate licenses under reasonable and non-discriminatory terms and 
conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications 
thereof for the sole  purpose of developing theISO/IEC 23002-4

IETR/INSA of Rennes retains full right to modify and use the 
code for its own purpose, assign or donate the code to a third party and 
to inhibit third parties from using the code for products that do not 
conform to MPEG-related ITU Recommendations and/or ISO/IEC International 
Standards. 

This copyright notice must be included in all copies or derivative works. 
Copyright (c) ISO/IEC 2008.
*****************************************************************************/

//Modified by Mederic Blestel and Endri Bezati IETR/INSA of Rennes
//Modified by Mickael Raulet IETR/INSA of Rennes
//Modified by Damien de Saint Jorre IETR/INSA of Rennes

//To do : 
//        delete ValidxRefList
//        delete ValidxRefList2
//        delete NbPOC2
//        delete IdxFalsePic
//        delete IdxNotFound42
actor Algo_RefList ()
	uint(size=31) RefReordering,
	uint(size=31) FraNum,
	uint(size=31) MaxFrameNum,
	uint(size=31) LongTermList,
	uint(size=31) ShortTermList,
	uint(size=4)  SzShortTermList,
	uint(size=5)  NbRefFrame,
	uint(size=32) POC,
	uint(size=2)  RefListType,
	bool          IsIDR
		==>
	int (size=32) RefList
		:

/*
*
* Constants
*
*/
	uint(size=2) REFLIST0_PRED    = 0;
	uint(size=2) REFLIST0_BI_PRED = 1;
	uint(size=2) REFLIST1_BI_PRED = 2;

/*
*
* State variables
*
*/
	List(type:int (size=32), size=32) FrameNumList := [ 0     : for int s in 0 .. 31];
	List(type:int (size=32), size=32) IdxLTList    := [-1     : for int s in 0 .. 31];
	List(type:int (size=32), size=32) POCList      := [ 0     : for int s in 0 .. 31];
	List(type:bool         , size=32) PicIsUsed    := [ false : for int s in 0 .. 31];

	List(type:uint(size=5) , size=32) RefIdx       := [ 0 : for int s in 0 .. 31];
	List(type:int (size=32), size=32) FrameNumWrap := [-1 : for int s in 0 .. 31];
	List(type:int (size=6) , size=16) ValLTList    := [-1 : for int s in 0 .. 15];

bool DEBUG;

	int (size=32) ValMovedFrame            := 0;
	uint(size=31) ValMaxFrameNum           := 0;
	uint(size=5)  CounterRefListTab        := 0; /* Contains the number of RefList's element which are sent                   */
	uint(size=5)  NbLTRead                 := 0; /* Contains the number of Long Term List's' element which are read           */
	uint(size=5)  NbSTRead                 := 0; /* Contains the number of Long Term List's' element which are read           */
	uint(size=5)  NbSTCount                := 0; /* Contains the number of Long Term List's' element which are read           */
	uint(size=5)  NumMaxListRefFrame       := 0; /* Frame Number of all pictures must be between 0 and NumMaxListRefFrame - 1 */
	uint(size=5)  IdxCurrentPic            := 0;
	uint(size=6)  NbPOC                    := 0;
	int (size=32) LastFrameNum             := 0;
	uint(size=2)  ListType                 := REFLIST0_PRED;

	/*Delete this variables !!!*/
	int(size=5)  ValidxRefList             := 0;
	bool         idx_found                 := false;
	bool         frame_found := false;
	int(size=32) ValMovedIdx                 := 0;


/*
*
* Procedure and Fontion
*
*/

	procedure InsertPoc(uint(size=6) IdxPOC, int(size=32) ValPOC, int(size=32) ValFrameNum, int(size=32) ValIdxLT)
		var
			uint(size=6) ValIdxMax
		begin
			if(NbPOC < 32) then
				ValIdxMax := NbPOC;
			else
				ValIdxMax := 31;
			end
			if(ValIdxMax>IdxPOC) then
				foreach int i in 0 .. ValIdxMax - IdxPOC - 1
				do
					POCList     [ValIdxMax - i] := POCList     [ValIdxMax - i - 1];
					FrameNumList[ValIdxMax - i] := FrameNumList[ValIdxMax - i - 1];
					IdxLTList   [ValIdxMax - i] := IdxLTList   [ValIdxMax - i - 1];
					PicIsUsed   [ValIdxMax - i] := PicIsUsed   [ValIdxMax - i - 1];
				end
			end
			POCList     [IdxPOC] := ValPOC;
			FrameNumList[IdxPOC] := ValFrameNum;
			IdxLTList   [IdxPOC] := ValIdxLT;

			NbPOC := NbPOC + 1;
			if(NbPOC > 32) then
				NbPOC := 32;
			end
		end


	procedure DelFrame(uint(size=32) ValFrame)
		var
			int(size=7) IdxToDel := -1
		begin
			foreach int i in 0 .. NbPOC - 1
			do
				if(FrameNumList[i] = ValFrame)then
					IdxToDel := i;
				end
			end
			if(IdxToDel != -1)then
				foreach int i in 0 .. NbPOC - IdxToDel - 2
				do
					POCList     [IdxToDel + i] := POCList     [IdxToDel + i + 1];
					FrameNumList[IdxToDel + i] := FrameNumList[IdxToDel + i + 1];
					IdxLTList   [IdxToDel + i] := IdxLTList   [IdxToDel + i + 1];
					PicIsUsed   [IdxToDel + i] := PicIsUsed   [IdxToDel + i + 1];
				end
				NbPOC := NbPOC - 1;
				if(IdxToDel <= IdxCurrentPic and IdxCurrentPic > 0) then
					IdxCurrentPic := IdxCurrentPic - 1;
				end
			end
		end


/*
*
* Actions
*
*/

	//Gets slice's FrameNum and slice's MaxFrameNum
	read_num_ref_frames: action
			FraNum          :[frame_num],
			POC             :[ValPOC],
			MaxFrameNum     :[MaxFrNu],
			IsIDR           :[IsIDR_Pic],
			SzShortTermList :[ValSzSTList],
			RefListType     :[ValRefListType]
				==>
		var
			bool IdxNotFound := true
		do
			LastFrameNum   := FrameNumList[IdxCurrentPic];
			ValMaxFrameNum := MaxFrNu;
			ListType       := ValRefListType;
DEBUG := false;//if(a)then true else false end;

			IdxCurrentPic := 0;
			while(IdxNotFound && IdxCurrentPic<32)
			do
				if((FrameNumList[IdxCurrentPic] > frame_num) or (IdxCurrentPic = NbPOC)) then
					IdxNotFound := false;
				end			
				IdxCurrentPic := IdxCurrentPic + 1;
			end
			IdxCurrentPic   := IdxCurrentPic   - 1;

			if(IdxCurrentPic >= 32) then
				IdxCurrentPic := 31;
			end

			if(not IsIDR_Pic) then
				DelFrame(frame_num);
				InsertPoc(IdxCurrentPic,ValPOC,frame_num,-1);
			else
				InsertPoc(0,0,0,-1);
				LastFrameNum  := 0;
				IdxCurrentPic := 0;
			end

			foreach int i in 0 .. NbPOC - 1
			do
				IdxLTList[i] := -1;
				PicIsUsed[i] := false;
			end
			NbLTRead       := 0;
			NbSTRead       := 0;
			NbSTCount      := ValSzSTList;
			ValMovedFrame  := frame_num;
if(DEBUG)then
		println("\n\n\n\n\n***************** New Picture ************************");
		println("Frame Num Curr Pic:"+frame_num+" Poc:"+ValPOC+" -> RefListType:"+ListType);
		println("");
end
		end


	//Fill the Long Term List
	ReadLists.LongTerm: action
			LongTermList :[ValLongTerm]
				==>
		guard
			NbLTRead < 16
		var
			bool idx_found2
		do
			frame_found := false;
			foreach int i in 0 .. NbPOC - 1
			do
				if(FrameNumList[i] = ValLongTerm) then
					IdxLTList[i] := NbLTRead;
					PicIsUsed[i] := true;
					frame_found  := true;
				end
			end
			idx_found := false;
			if(not frame_found) then
				foreach int i in 0 .. NbPOC - 1
				do
					idx_found2 :=idx_found;
					if(FrameNumList[i] > ValLongTerm and idx_found = false) then
						idx_found2 := true;
						InsertPoc(i, -42, ValLongTerm, NbLTRead);
						if(i<= IdxCurrentPic and IdxCurrentPic<31) then
							IdxCurrentPic := IdxCurrentPic + 1;
						end
					end
					idx_found :=idx_found2;
				end
			end
			NbLTRead     := NbLTRead + 1;
		end


	//Fill the Long Term List
	ReadLists.ShortTerm: action
			ShortTermList :[ValShortTerm]
				==>
		guard
			NbSTRead < NbSTCount
		var
			bool idx_found2
		do
			frame_found := false;
			foreach int i in 0 .. NbPOC - 1
			do
				if(FrameNumList[i] = ValShortTerm) then
					PicIsUsed[i] := true;
					frame_found  := true;
				end
			end
			idx_found := false;
			//Insert False Frame
			if(not frame_found) then
				foreach int i in 0 .. NbPOC - 1
				do
					idx_found2 :=idx_found;
					if(FrameNumList[i] > ValShortTerm and idx_found = false) then
						idx_found2 := true;
						InsertPoc(i, -42, ValShortTerm, -1);
						if(i<= IdxCurrentPic and IdxCurrentPic<31) then
							IdxCurrentPic := IdxCurrentPic + 1;
						end
					end
					idx_found :=idx_found2;
				end
			end
			NbSTRead     := NbSTRead + 1;
		end


	//Long Term and Short Term are read
	ReadLists.done: action ==>
		var
			uint(size=5) idx       := 0,
			uint(size=6) NbPOCtemp := 0
		do
			//I use NBSTRead variable because of the bug in frontend which will be solved quickly. 
			NbSTRead := 0;
			NbPOCtemp := NbPOC;
			while((idx < NbPOCtemp) and (NbPOCtemp > 0))
			do
				idx := NbSTRead;
				if((PicIsUsed[idx] = false) and (idx != IdxCurrentPic))then
					DelFrame(FrameNumList[idx]);
				else
					idx := idx + 1;
				end
				NbSTRead  := idx;
				NbPOCtemp := NbPOC;
			end
if(DEBUG)then
	println("");
end
		end


	//Get the Length of the Reference Frame (and send it throw RefList) and initialize the RefListTab
	InitRefList.RefList0_Pred: action
		NbRefFrame :[ValIDxMax]
			==>
		RefList      :[NumMaxListRefFrame]
	guard
		ListType = REFLIST0_PRED
	var
			uint(size=4) IdxRefList2,
			uint(size=5) IdxBuffer,
			int (size=32) FrameNumBuffer
	do
	if(DEBUG)then
		println("P frame");
	end
		//IdxCurrentPic = Nb short term and long term ref
		if(ValIDxMax < NbPOC - 1) then
			NumMaxListRefFrame := ValIDxMax;
		else
			NumMaxListRefFrame := NbPOC - 1;
		end

		foreach int i in 0 .. 15
		do
			ValLTList[i] := -1;
		end
ValidxRefList := 0;//////
		foreach int i in 0 .. NbPOC - 1
		do
			IdxRefList2 := ValidxRefList; 
			if(i != IdxCurrentPic)then
				if(IdxLTList[i] = -1)then
					RefIdx[IdxRefList2] := i;
					if(FrameNumList[i] > FrameNumList[IdxCurrentPic]) then
						FrameNumWrap[IdxRefList2] := FrameNumList[i] - ValMaxFrameNum;
					else
						FrameNumWrap[IdxRefList2] := FrameNumList[i];
					end
					IdxRefList2         := IdxRefList2 + 1;
				else
					ValLTList[IdxLTList[i]] := i;
				end
			end
			ValidxRefList := IdxRefList2;
		end
		if(ValidxRefList>0) then
			foreach int i in 0 .. ValidxRefList - 1
			do
				foreach int j in i + 1 .. ValidxRefList - 1
				do
					if(FrameNumWrap[i] < FrameNumWrap[j])then
						IdxBuffer       := RefIdx[j];
						RefIdx[j]       := RefIdx[i];
						RefIdx[i]       := IdxBuffer;

						FrameNumBuffer  := FrameNumWrap[j];
						FrameNumWrap[j] := FrameNumWrap[i];
						FrameNumWrap[i] := FrameNumBuffer;
					end
				end
			end
		end
if(DEBUG)then
		println("Buffer POCLIst and FrameNumList:");
		foreach int i in 0 .. NbPOC - 1
		do
			println("FrameNum:"+FrameNumList[i]+" Poc:"+POCList[i]);
		end
		println("\nSizeBuffer:"+NumMaxListRefFrame+"; Originaly:"+ValIDxMax+"\nShortTermPart");
end
		if(ValidxRefList < NumMaxListRefFrame)then
			foreach int i in 0 .. 15
			do
				IdxRefList2 := ValidxRefList; 
				if ( (ValLTList[i] != -1) and (IdxRefList2 < NumMaxListRefFrame) ) then 
					RefIdx[IdxRefList2] := ValLTList[i]; 
					IdxRefList2         := IdxRefList2 + 1 ;
				end
				ValidxRefList := IdxRefList2;
			end
		end
		//Indicates that nothing in RefList has been sent
		CounterRefListTab := 0;
	end


	//Get the Length of the Reference Frame (and send it throw RefList) and initialize the RefListTab
	InitRefList.RefList0_Bi_Pred: action
		NbRefFrame :[ValIDxMax]
			==>
		RefList      :[NumMaxListRefFrame]
	var
			uint(size=4)                     IdxRefList    := 0,
			uint(size=5)                     j             := 0,
			List(type:uint(size=4), size=16) RefSTList,
			uint(size=4)                     SzRefSTList   := 0,
			int (size=32)                    RefBuffer,
			uint(size=4)                     IdxLastPocInf := -1
	do
	if(DEBUG)then
		println("B frame");
	end
		//IdxCurrentPic = Nb short term and long term ref
		if(ValIDxMax < NbPOC - 1) then
			NumMaxListRefFrame := ValIDxMax;
		else
			NumMaxListRefFrame := NbPOC - 1;
		end

		
		foreach int i in 0 .. NbPOC - 1
		do
			if( (IdxLTList[i] = -1) and (i != IdxCurrentPic) )then
				RefSTList[SzRefSTList] := i;
				SzRefSTList            := SzRefSTList + 1;
			end
		end
		if(SzRefSTList > 0)then
			foreach int i in 0 .. SzRefSTList - 1
			do
				foreach int k in i .. SzRefSTList - 1
				do
					if(POCList[RefSTList[i]] > POCList[RefSTList[k]])then
						RefBuffer    := RefSTList[i];
						RefSTList[i] := RefSTList[k];
						RefSTList[k] := RefBuffer;
					end
				end
				if(POCList[RefSTList[i]] < POCList[IdxCurrentPic])then
					IdxLastPocInf := i;
				end
			end
		end
		if(IdxLastPocInf >= 0)then
			foreach int i in 0 .. IdxLastPocInf
			do
				RefIdx[IdxRefList] := RefSTList[IdxLastPocInf - i];
				IdxRefList         := IdxRefList + 1;
			end
			foreach int i in IdxLastPocInf + 1 .. SzRefSTList - 1
			do
				RefIdx[IdxRefList] := RefSTList[i];
				IdxRefList         := IdxRefList + 1;
			end
		else
			foreach int i in 0 .. SzRefSTList - 1
			do
				RefIdx[IdxRefList] := RefSTList[i];
				IdxRefList         := IdxRefList + 1;
			end
		end

		while( (j < 16) and (IdxRefList < NumMaxListRefFrame) )
		do
			if(ValLTList[j] != -1)then
					RefIdx[IdxRefList] := ValLTList[j]; 
					IdxRefList         := IdxRefList + 1 ;
			end
			j := j + 1;
		end

if(DEBUG)then
		println("Buffer POCLIst and FrameNumList:");
		foreach int i in 0 .. NbPOC - 1
		do
			println("FrameNum:"+FrameNumList[i]+" Poc:"+POCList[i]);
		end
		println("\nSizeBuffer:"+NumMaxListRefFrame+"; Originaly:"+ValIDxMax+"\nShortTermPart");
end
		//Indicates that nothing in RefList has been sent
		CounterRefListTab := 0;
	end


	//If we don't have to reorder the list, we jump to SendRefList (see scheduler)
	read_ref_pic_list_reordering_flag_0: action 
			RefReordering :[reordering_flag]
				==>
		guard
			reordering_flag = 0
		end


	//We have to reorder the list
	read_ref_pic_list_reordering_flag_1: action 
			RefReordering :[reordering_flag]
				==> 
		guard
			reordering_flag = 1
		end


	//A Short Term Reference must be reordered
	read_reordering_of_pic_nums_idc_0or1: action
			RefReordering :[reordering_of_pic_nums_idc, abs_diff_pic_num_minus1]
				==>
			RefList:[poc]
		guard 
			(reordering_of_pic_nums_idc = 0 or reordering_of_pic_nums_idc = 1) 
		var
			bool IsInRefList := false,
			int (size = 32) poc
		do
			//Compute the value of the re-mapped reference picture
			if (reordering_of_pic_nums_idc = 0) then 
				ValMovedFrame := ValMovedFrame - abs_diff_pic_num_minus1 - 1 ; 
				if(ValMovedFrame < 0) then
					ValMovedFrame := ValMovedFrame + ValMaxFrameNum;
				end
			else // if(a =1) then
				ValMovedFrame := ValMovedFrame + abs_diff_pic_num_minus1 + 1;
				if(ValMovedFrame >= ValMaxFrameNum) then
					ValMovedFrame := ValMovedFrame - ValMaxFrameNum;
				end
			end
		ValMovedIdx  := 0;
			//Search the location of the reference picture (ValMovedFrame) in the RefList
			if(CounterRefListTab < NumMaxListRefFrame)then
				foreach int i in CounterRefListTab .. NumMaxListRefFrame-1
				do
					if (FrameNumList[RefIdx[i]] = ValMovedFrame) then 
						ValidxRefList  := i;
						ValMovedIdx := RefIdx[i];
						IsInRefList := true;
					end
				end
			end
			if(IsInRefList) then
				//Shift to the right all the element with an index between 
				//IdxInsertFrame and idxRefList - 1 (IdxInsertFrame and idxRefList - 1 are also shifted).
				//Thanks to this shift, the short term reference picture located at idxRefList is deleted.
				foreach int i in 0 .. ValidxRefList - CounterRefListTab - 1
				do
					RefIdx[ValidxRefList - i] := RefIdx[ValidxRefList - i - 1];
				end
				//We send the Poc value of the next element
				poc := POCList[ValMovedIdx];
				CounterRefListTab := CounterRefListTab + 1;
			else
				foreach int i in 0 .. NbPOC - 1
				do
					if (FrameNumList[i] = ValMovedFrame and i !=IdxCurrentPic) then 
						ValMovedIdx    := i;
					end
				end
				poc := POCList[ValMovedIdx];
				CounterRefListTab := CounterRefListTab + 1;
				NumMaxListRefFrame := NumMaxListRefFrame - 1;
			end

		end


	//A Long Term Reference must be reordered
	read_reordering_of_pic_nums_idc_2: action
			RefReordering:[reordering_of_pic_nums_idc, idx_LTList]
				==>
			RefList:[poc]
		guard 
			reordering_of_pic_nums_idc = 2
		var
			int (size = 32) poc
		do
			ValidxRefList := 0;
			//Search the location of the reference picture in the RefList
			foreach int i in 0 .. NumMaxListRefFrame-1
			do
				if (RefIdx[i] = ValLTList[idx_LTList]) then 
					ValidxRefList := i;
				end
			end
			//Shift to the right all the element with an index between 
			//IdxInsertFrame and idxRefList - 1 (IdxInsertFrame and idxRefList - 1 are also shifted).
			//Thanks to this shift, the long term reference picture located at idxRefList is deleted.
			if(ValidxRefList > CounterRefListTab) then
				foreach int i in 0 .. ValidxRefList - CounterRefListTab - 1
				do
					RefIdx[ValidxRefList - i] := RefIdx[ValidxRefList - i - 1];
				end
			end

			//We send the Poc value of the next element
			poc := POCList[ValLTList[idx_LTList]];
			CounterRefListTab := CounterRefListTab + 1;
		end


	//Reordering is completed
	read_reordering_of_pic_nums_idc_3: action
			RefReordering:[reordering_of_pic_nums_idc]
				==>
		guard 
			reordering_of_pic_nums_idc = 3
		end


	//Send the RefList
	SendRefList: action 
				==>
			RefList:[poc]
		guard
			CounterRefListTab < NumMaxListRefFrame 
		var
			int (size = 32) poc := -1
		do
			poc := POCList[RefIdx[CounterRefListTab]];
if(DEBUG)then
			println("RefListSent:"+FrameNumList[RefIdx[CounterRefListTab]]+"; Poc:"+POCList[RefIdx[CounterRefListTab]]);
end
			CounterRefListTab := CounterRefListTab + 1;
		end


	//The RefList is sent
	SendRefListDone: action ==>
		end

/*
*
* Scheduler
*
*/
	schedule fsm  read_num_ref_frames:

		read_num_ref_frames               (read_num_ref_frames                  )--> ReadLists;

		ReadLists                         (ReadLists.ShortTerm                  )--> ReadLists;
		ReadLists                         (ReadLists.LongTerm                   )--> ReadLists;
		ReadLists                         (ReadLists.done                       )--> InitRefList;

		InitRefList                       (InitRefList                          )--> read_ref_pic_list_reordering_flag;

		read_ref_pic_list_reordering_flag (read_ref_pic_list_reordering_flag_0  )--> SendRefList;

		read_ref_pic_list_reordering_flag (read_ref_pic_list_reordering_flag_1  )--> read_reordering_of_pic_nums_idc;

		read_reordering_of_pic_nums_idc   (read_reordering_of_pic_nums_idc_0or1 )--> read_reordering_of_pic_nums_idc;
		read_reordering_of_pic_nums_idc   (read_reordering_of_pic_nums_idc_2    )--> read_reordering_of_pic_nums_idc;
		read_reordering_of_pic_nums_idc   (read_reordering_of_pic_nums_idc_3    )--> SendRefList;

		SendRefList                       (SendRefList                          )--> SendRefList;
		SendRefList                       (SendRefListDone                      )--> read_num_ref_frames;


	end


	priority
		ReadLists.LongTerm        > ReadLists.ShortTerm       > ReadLists.done;
		SendRefList               > SendRefListDone;
		InitRefList.RefList0_Pred > InitRefList.RefList0_Bi_Pred;
	end

end
