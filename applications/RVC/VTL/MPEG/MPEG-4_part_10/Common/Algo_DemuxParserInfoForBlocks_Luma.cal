/********************************************************************************

This software module was originally developed by Damien de Saint Jorre IETR/INSA of Rennes (France), in the course of development of the <standard> for reference purposes and its performance may not have been optimized. This software module is an implementation of one or more tools as specified by the MPEG-C part 4.
ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works of this software module or modifications thereof for use in implementations of the <standard>  in products that satisfy conformance criteria (if any).
Those intending to use this software module in products are advised that its use may infringe existing patents. ISO/IEC have no liability for use of this software module or modifications thereof.
Copyright is not released for products that do not conform to audiovisual and image-coding related ITU Recommendations and/or ISO/IEC International Standards.
Assurance that the originally developed software module can be used (1) in the <standard> once the <standard> has been adopted; and (2) to develop the MPEG-C part 4:

IETR/INSA of Rennes (France) grants ISO/IEC all rights necessary to include the originally developed software module or modifications thereof in the <standard> and to permit ISO/IEC to offer You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works for use in implementations of the MPEG-C part 4 in products that satisfy conformance criteria (if any), and to the extent that such originally developed software module or portions of it are included in the MPEG-C part 4.  To the extent that IETR/INSA of Rennes (France) owns patent rights that would be required to make, use, or sell the originally developed software module or portions thereof included in the MPEG-C part 4 in a conforming product,  IETR/INSA of Rennes (France) will assure the ISO/IEC that it is willing to negotiate licenses under reasonable and non-discriminatory terms and conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications thereof for the sole  purpose of developing the MPEG-C part 4

IETR/INSA of Rennes (France) retains full right to modify and use the code for its own purpose, assign or donate the code to a third party and to inhibit third parties from using the code for products that do not conform to MPEG-related ITU Recommendations and/or ISO/IEC International Standards.
This copyright notice must be included in all copies or derivative works. Copyright (c) ISO/IEC 2008.

*****************************************************************************/
actor Algo_DemuxParserInfoForBlocks_Luma()
	uint(size=6)  MBType,
	bool          TransformSize8x8,
	int (size=16) Coef_ACR,
	uint(size=13) FirstMbInSlice,
	bool          ConstrainedIFlag,
	uint(size=4)  PredMode
		==>
	int (size=16) Coef_ACR_Intra_4x4,
	uint(size=13) FstMbInSlice_Intra_4x4,
	bool          ConstrI_Intra_4x4,
	uint(size=4)  PredMode_4x4,

//	int(size=16)  Coef_ACR_Intra_8x8,

	int (size=16) Coef_ACR_Intra_16x16,
	uint(size=13) FstMbInSlice_Intra_16x16,
	bool          ConstrI_Intra_16x16,
	uint(size=4)  PredMode_16x16,

	int(size=16)  Coef_ACR_Inter
		:


/*
*
* Constants
*
*/
	uint(size=6) BLOCK_TYPE_I_8x8_or_4x4    = 0;
	uint(size=6) BLOCK_TYPE_INTRA_16x16_MIN = 1;
	uint(size=6) BLOCK_TYPE_INTRA_16x16_MAX = 24;
	uint(size=6) BLOCK_TYPE_I_PCM           = 25;
	uint(size=6) MB_TYPE_INTRA_MAX          = 25;



	bool AcrSent;
	bool FirstMbSent;
	bool ConstrISent;
	bool PredModeSent;
/*
*
* Actions
*
*/

	MBTypeIsPCM: action
			MBType           :[ValMBType],
			FirstMbInSlice   :[FirstMb],
			ConstrainedIFlag :[c_intra_flag]
				==>
		guard
			ValMBType = BLOCK_TYPE_I_PCM
		end


	MBTypeIsIntra4x4: action
			MBType           :[ValMBType],
			TransformSize8x8 :[Is8x8]
				==>
		guard
			ValMBType    = BLOCK_TYPE_I_8x8_or_4x4,
			Is8x8        = false
		do
			AcrSent      := false;
			FirstMbSent  := false;
			ConstrISent  := false;
			PredModeSent := false;
		end

	SendAcr4x4: action
			Coef_ACR           :[ValCoef] repeat 256
				==>
			Coef_ACR_Intra_4x4 :[ValCoef] repeat 256
		guard
			not AcrSent
		do
			AcrSent := true;
		end

	SendFstMb4x4: action
			FirstMbInSlice         :[FirstMb]
				==>
			FstMbInSlice_Intra_4x4 :[FirstMb]
		guard
			not FirstMbSent
		do
			FirstMbSent := true;
		end

	SendCnstrI4x4: action
			ConstrainedIFlag  :[c_intra_flag]
				==>
			ConstrI_Intra_4x4 :[c_intra_flag]
		guard
			not ConstrISent
		do
			ConstrISent := true;
		end

	SendPredMode4x4: action
			PredMode     :[pred_mode] repeat 16
				==>
			PredMode_4x4 :[pred_mode] repeat 16
		guard
			not PredModeSent
		do
			PredModeSent := true;
		end

	SendInfosDone: action ==>
		guard
			AcrSent,
			FirstMbSent,
			ConstrISent,
			PredModeSent
		end

	MBTypeIsIntra8x8: action
			MBType           :[ValMBType],
			TransformSize8x8 :[Is8x8]
				==>
		guard
			ValMBType = BLOCK_TYPE_I_8x8_or_4x4,
			Is8x8     = true
		do
			println("Algo_Demux_Coef_ACR MBTypeIsIntra8x8 not implemented yet !!");
		end


	MBTypeIsIntra16x16: action
			MBType           :[ValMBType]
				==>
		guard
			ValMBType >= BLOCK_TYPE_INTRA_16x16_MIN,
			ValMBType <= BLOCK_TYPE_INTRA_16x16_MAX
		do
			AcrSent      := false;
			FirstMbSent  := false;
			ConstrISent  := false;
			PredModeSent := false;
		end

	SendAcr16x16: action
			Coef_ACR             :[ValCoef] repeat 256
				==>
			Coef_ACR_Intra_16x16 :[ValCoef] repeat 256
		guard
			not AcrSent
		do
			AcrSent := true;
		end

	SendFstMb16x16: action
			FirstMbInSlice         :[FirstMb]
				==>
			FstMbInSlice_Intra_16x16 :[FirstMb]
		guard
			not FirstMbSent
		do
			FirstMbSent := true;
		end

	SendCnstrI16x16: action
			ConstrainedIFlag  :[c_intra_flag]
				==>
			ConstrI_Intra_16x16      :[c_intra_flag]
		guard
			not ConstrISent
		do
			ConstrISent := true;
		end

	SendPredMode16x16: action
			PredMode       :[pred_mode]
				==>
			PredMode_16x16 :[pred_mode]
		guard
			not PredModeSent
		do
			PredModeSent := true;
		end


	MBTypeIsInter: action
			MBType           :[ValMBType]
				==>
		guard
			ValMBType > MB_TYPE_INTRA_MAX
		do
			AcrSent      := false;
			FirstMbSent  := false;
			ConstrISent  := false;
			PredModeSent := true;
		end

	SendAcrInter: action
			Coef_ACR       :[ValCoef] repeat 256
				==>
			Coef_ACR_Inter :[ValCoef] repeat 256
		guard
			not AcrSent
		do
			AcrSent := true;
		end

	GetCnstrIAndFstMbInter: action
			ConstrainedIFlag :[c_intra_flag],
			FirstMbInSlice   :[FirstMb]
				==>
		guard
			not FirstMbSent
		do
			FirstMbSent := true;
			ConstrISent := true;
		end

	schedule fsm CheckMbType:
		CheckMbType         (MBTypeIsPCM            )--> CheckMbType;
		CheckMbType         (MBTypeIsIntra4x4       )--> SendIntra4x4Infos;
		CheckMbType         (MBTypeIsIntra8x8       )--> undefined;
		CheckMbType         (MBTypeIsIntra16x16     )--> SendIntra16x16Infos;
		CheckMbType         (MBTypeIsInter          )--> SendInterInfos;

		SendIntra4x4Infos   (SendAcr4x4             )--> SendIntra4x4Infos;
		SendIntra4x4Infos   (SendFstMb4x4           )--> SendIntra4x4Infos;
		SendIntra4x4Infos   (SendCnstrI4x4          )--> SendIntra4x4Infos;
		SendIntra4x4Infos   (SendPredMode4x4        )--> SendIntra4x4Infos;
		SendIntra4x4Infos   (SendInfosDone          )--> CheckMbType;

		SendIntra16x16Infos (SendAcr16x16           )--> SendIntra16x16Infos;
		SendIntra16x16Infos (SendFstMb16x16         )--> SendIntra16x16Infos;
		SendIntra16x16Infos (SendCnstrI16x16        )--> SendIntra16x16Infos;
		SendIntra16x16Infos (SendPredMode16x16      )--> SendIntra16x16Infos;
		SendIntra16x16Infos (SendInfosDone          )--> CheckMbType;

		SendInterInfos      (SendAcrInter           )--> SendInterInfos;
		SendInterInfos      (GetCnstrIAndFstMbInter )--> SendInterInfos;
		SendInterInfos      (SendInfosDone          )--> CheckMbType;
	end
end