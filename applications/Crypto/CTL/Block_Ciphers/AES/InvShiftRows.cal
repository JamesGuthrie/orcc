/**
 * This FU is part of a CAL implementation of AES (Rijndael).
 * It performs InvShiftRows process in each round of the decipher.
 * 
 * FU ID: InvShiftRows
 * One input port:
 *   1) uint(size=8) IP: the input
 * One output port:
 *   1) uint(size=8) OP: the output
 *
 * Developer: Shujun Li @ Uni-Konstanz 2010
 */

package Block_Ciphers.AES;

actor InvShiftRows () uint(size=8) IP ==> uint(size=8) OP:

	List(type:int, size=16) INDICES = 
	[ // Note that the plaintext is columnwise.
		 0,13,10, 7,
		 4, 1,14,11,
		 8, 5, 2,15,
		12, 9, 6, 3
	];

	List(type:uint(size=8), size=17) y := [0 : for int i in 1 .. 17];

	action IP:[x] repeat 17 ==> OP:[y] repeat 17
	do
		y[0] := x[0] + 1;
		foreach int i in 1 .. 16
		do
			y[i] := x[INDICES[i-1]+1];
		end
	end

end
